buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = 'android-maps-utils'
group = 'de.quist.app.maps'

dependencies {
    compile 'com.android.support:support-v4:21.+'
    compile 'de.quist.apps.maps:android-maps-abstraction:1.0.0'
    androidTestCompile 'de.quist.apps.maps:google-maps-binding:1.0.0'
    androidTestCompile 'com.google.android.gms:play-services:6.5.87'
}


android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }

        androidTest {
            java.srcDirs = ['tests/src']
        }
    }
}

task instrumentTest(dependsOn: connectedCheck)

task apklib(type: Zip) {
    dependsOn 'packageReleaseJar'
    appendix = extension = 'apklib'

    from 'AndroidManifest.xml'
    into('res') {
        from 'res'
    }
    into('src') {
        from 'src'
    }
}

artifacts {
    archives apklib
}

signing {
    sign configurations.archives
}

if(!hasProperty("sonatypeUsername")){
    sonatypeUsername = ""
}
if(!hasProperty("sonatypePassword")){
    sonatypePassword = ""
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            modifyPom(addFilter('aar') { artifact, file ->
                artifact.name == 'android-maps-utils'
            })

            modifyPom(addFilter('apklib') { artifact, file ->
                artifact.name == 'android-maps-utils-apklib'
            })

            // There's no official apklib for Google Play services, so we
            // can't really maintain that dependency for the apklib packaging.
            pom('apklib').whenConfigured { pom ->
                pom.dependencies = []
            }
        }
    }
}

def modifyPom(pom) {
    pom.project {
        name 'Android Maps API utility library for maps-abstraction-library'
        description 'Handy extensions to the android-maps-abstraction library.'
        url 'https://github.com/tomquist/android-maps-utils'

        scm {
            url 'scm:git@github.com:tomquist/android-maps-utils.git'
            connection 'scm:git@github.com:tomquist/android-maps-utils.git'
            developerConnection 'scm:git@github.com:tomquist/android-maps-utils.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'broady'
                name 'Chris Broadfoot'
                url 'http://google.com/+ChristopherBroadfoot'
            }
            developer {
                id 'tomquist'
                name 'Tom Quist'
            }
        }
    }
}
